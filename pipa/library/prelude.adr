protocol Maybe(a) {}

struct Some(a) is Maybe(a) {
    data: a
}

struct None is Maybe(a) {}


struct String {
    data: py#Str
    iterIndex: py#Int

    fun __init__(data: py#Str): Void {
        self.data = data
        self.iterIndex = py#Int(0)
    }
}

struct Number {
    data: py#Int
}

struct Vector(a) {
    data: py#List
    iterIndex: py#Int

    fun __init__(data: py#List): Void {
        self.data = data
        self.iterIndex = py#Int(0)
    }
}

struct Set(a) {
    data: py#Set
}

struct Dict(a, b) {
    data: py#Dict
}


protocol ConvertibleToString {
    fun __toString__(): String
}

protocol ConvertibleToNumber {
    fun __toNumber__(): Number
}

protocol ConvertibleToVector(a) {
    fun __toVector__(): Vector(a)
}

protocol ConvertibleToSet(a) {
    fun __toSet__(): Set(a)
}

protocol ConvertibleToDict(a, b) {
    fun __toDict__(): Dict(a, b)
}

protocol Addable {
    fun __add__(other: Self): Self
}

protocol Subtractable {
    fun __sub__(other: Self): Self
}

protocol Multipliable {
    fun __mul__(other: Self): Self
}

protocol Divisible {
    fun __div__(other: Self): Self
}

protocol Eq {
    fun __eq__(other: Self): Bool
}

protocol Lt {
    fun __lt__(other: Self): Bool
}

protocol Gt {
    fun __gt__(other: Self): Bool
}

protocol Container(a) {
    fun __contains__(element: a): Bool
}

protocol Printable {
    fun __print__(): Void
}

protocol Lengthable {
    fun __length__(): Number
}

protocol Iterable(a) {
    fun __next__(): Maybe(a)
}

protocol GetItemable(a) {
    fun __getItem__(index: Number): Maybe(a)
}

protocol SetItemable(a) {
    fun __setItem__(index: Number, item: a): Void
}


fun toString(data: ConvertibleToString): String {
    return data.__toString__()
}

fun toNumber(data: ConvertibleToNumber): Number {
    return data.__toNumber__()
}

fun toVector(data: ConvertibleToVector(a)): Vector(a) {
    return data.__toVector__()
}

fun toSet(data: ConvertibleToSet(a)): Set(a) {
    return data.__toSet__()
}

fun toDict(data: ConvertibleToDict(a, b)): Dict(a, b) {
    return data.__toDict__()
}

fun print(data: ConvertibleToString): Void {
    py#print(toString(data).data)
}

fun length(data: Lengthable): Number {
    return data.__length__()
}


extension Number is ConvertibleToNumber, ConvertibleToString, Addable,
                    Subtractable, Multipliable, Divisible, Eq, Lt, Gt {
    fun __toNumber__(): Number {
        return self
    }

    fun __toString__(): String {
        return String(py#toStr(self.data))
    }

    fun __add__(other: Number): Number {
        return Number(self.data + other.data)
    }

    fun __sub__(other: Number): Number {
        return Number(self.data - other.data)
    }

    fun __mul__(other: Number): Number {
        return Number(self.data * other.data)
    }

    fun __div__(other: Number): Number {
        return Number(self.data / other.data)
    }

    fun __eq__(other: Number): Bool {
        return self.data == other.data
    }

    fun __lt__(other: Number): Bool {
        return self.data < other.data
    }

    fun __gt__(other: Number): Bool {
        return self.data > other.data
    }
}

extension String is ConvertibleToNumber, ConvertibleToString,
                    ConvertibleToVector(String), ConvertibleToSet(String),
                    Addable, Lengthable, GetItemable(String), Eq,
                    Container(String), Iterable(String) {
    fun __add__(other: String): String {
        return String(self.data + other.data)
    }

    fun __eq__(other: String): Bool {
        return self.data == other.data
    }

    fun __contains__(element: String): Bool {
        return element.data in self.data
    }

    fun __toNumber__(): Number {
        return Number(py#toInt(self.data))
    }

    fun __toString__(): String {
        return self
    }

    fun __toVector__(): Vector(String) {
        return Vector(py#toList(self.data))
    }

    fun __toSet__(): Set(String) {
        return Set(py#toSet(self.data))
    }

    fun __length__(): Number {
        return Number(py#len(self.data))
    }

    fun __getItem__(index: Number): Maybe(String) {
        let got = self.data[index.data]
        if got is py#None {
            return None()
        }
        return Some(got)
    }

    fun __next__(): Maybe(String) {
        let element = self.data[self.iterIndex]
        if element is py#None {
            self.iterIndex = py#Int(0)
            return None()
        }
        self.iterIndex += py#Int(1)
        return Some(String(element))
    }
}

extension Vector(a) is ConvertibleToVector(a), ConvertibleToString,
                    ConvertibleToSet(a), Addable, Lengthable, GetItemable(a),
                    SetItemable(a), Eq, Container(a), Iterable(a) {
    fun __add__(other: Vector(a)): Vector(a) {
        return Vector(self.data + other.data)
    }

    fun __eq__(other: Vector(a)): Bool {
        return self.data == other.data
    }

    fun __contains__(element: a): Bool {
        return element in self.data
    }

    fun __toVector__(): Vector(a) {
        return self
    }

    fun __toString__(): String {
        let separator = py#Str(", ")
        let dataLength = length(self).data
        var index = py#Int(0)
        var result = py#Str("[")
        for element in self.data {
            result += toString(element).data
            if index < (dataLength - py#Int(1)) {
                result += separator
            }
            index += py#Int(1)
        }
        return String(result + py#Str("]"))
    }

    fun __toSet__(): Set(a) {
        return Set(py#toSet(self.data))
    }

    fun __length__(): Number {
        return Number(py#len(self.data))
    }

    fun __getItem__(index: Number): Maybe(a) {
        let got = self.data[index.data]
        if got is py#None {
            return None()
        }
        return Some(got)
    }

    fun __setItem__(index: Number, item: a): Void {
        self.data[index.data] = item
    }

    fun __next__(): Maybe(a) {
        let element = self.data[self.iterIndex]
        if element is py#None {
            self.iterIndex = py#Int(0)
            return None()
        }
        self.iterIndex += py#Int(1)
        return Some(element)
    }

    fun append(element: a): Void {
        self.data += py#List([element])
    }

    fun extend(other: Vector(a)): Void {
        self.data += other
    }
}
